; --------------------------------------------------
; Add the 32 bit values pointed to by ZERO_PAGE_1 and ZERO_PAGE_3
; and store the result in ZERO_PAGE_3 
;
; op3 <- op1 + op3
; --------------------------------------------------
add32BitUnsigned
    ldy #0
    clc
    lda (ZERO_PAGE_1),y
    adc (ZERO_PAGE_3),y
    sta (ZERO_PAGE_3),y
    iny
    lda (ZERO_PAGE_1),y
    adc (ZERO_PAGE_3),y
    sta (ZERO_PAGE_3),y    
    iny
    lda (ZERO_PAGE_1),y
    adc (ZERO_PAGE_3),y
    sta (ZERO_PAGE_3),y    
    iny
    lda (ZERO_PAGE_1),y
    adc (ZERO_PAGE_3),y
    sta (ZERO_PAGE_3),y    

    rts


; --------------------------------------------------
; XOR the 32 bit values pointed to by ZERO_PAGE_1 and ZERO_PAGE_3
; and store the result in ZERO_PAGE_3 
;
; op3 <- op1 xor op3
; --------------------------------------------------
xor32BitUnsigned
    ldy #0
    lda (ZERO_PAGE_1),y
    eor (ZERO_PAGE_3),y
    sta (ZERO_PAGE_3),y
    iny
    lda (ZERO_PAGE_1),y
    eor (ZERO_PAGE_3),y
    sta (ZERO_PAGE_3),y    
    iny
    lda (ZERO_PAGE_1),y
    eor (ZERO_PAGE_3),y
    sta (ZERO_PAGE_3),y    
    iny
    lda (ZERO_PAGE_1),y
    eor (ZERO_PAGE_3),y
    sta (ZERO_PAGE_3),y    

    rts

; --------------------------------------------------
; move 32 bit values pointed to by ZERO_PAGE_1 to addres to which ZERO_PAGE_3
; points. 
; 
; op3 <- op1
; --------------------------------------------------
move32Bit
    ldy #3
.loopMove
    lda (ZERO_PAGE_1),y
    sta (ZERO_PAGE_3),y
    dey
    bpl .loopMove

    rts


; --------------------------------------------------
; rot32Bit rotates the 32 bit value to which ZERO_PAGE_1 points one bit
; to the left.
;
; op1 <- op1 <<< 1
; --------------------------------------------------
rot32Bit
    ldy #0
    lda (ZERO_PAGE_1),y
    asl
    sta (ZERO_PAGE_1),y

    iny
    lda (ZERO_PAGE_1),y
    rol
    sta (ZERO_PAGE_1),y

    iny
    lda (ZERO_PAGE_1),y
    rol
    sta (ZERO_PAGE_1),y

    iny
    lda (ZERO_PAGE_1),y
    rol
    sta (ZERO_PAGE_1),y
    bcc .noCarry

    ldy #0
    lda (ZERO_PAGE_1),y
    ora #1
    sta (ZERO_PAGE_1),y    

.noCarry
    rts


; --------------------------------------------------
; print32Bit prints the 32 bit value to which ZERO_PAGE_1 points
; --------------------------------------------------
print32Bit
    ldy #3
.loopPrint
    lda (ZERO_PAGE_1),y
    jsr printByte
    dey
    bpl .loopPrint

    rts    


.SCRATCH_ROT_16_1
!byte 0
.SCRATCH_ROT_16_2
!byte 0
; --------------------------------------------------
; rot16Bits rotates the 32 bit value to which ZERO_PAGE_1 points 2 bytes
; to the left.
;
; op1 <- op1 <<< 16
; --------------------------------------------------
rot16Bits
    +move16Bit ZERO_PAGE_1, ZERO_PAGE_3
    +sub16BitImmediate 2, ZERO_PAGE_3

    ; save two MSB
    ldy #3
    lda (ZERO_PAGE_1),y
    sta .SCRATCH_ROT_16_1
    dey
    lda (ZERO_PAGE_1),y
    sta .SCRATCH_ROT_16_2

    ldy #3
    ; copy remaining two bytes
    lda (ZERO_PAGE_3), y
    sta (ZERO_PAGE_1), y 
    dey

    lda (ZERO_PAGE_3), y
    sta (ZERO_PAGE_1), y

    ; copy saved bytes
    ldy #1
    lda .SCRATCH_ROT_16_1
    sta (ZERO_PAGE_1),Y
    dey
    lda .SCRATCH_ROT_16_2
    sta (ZERO_PAGE_1), y     

    rts


.SCRATCH_ROT_8
!byte 0
; --------------------------------------------------
; rot8Bits rotates the 32 bit value to which ZERO_PAGE_1 points 1 byte
; to the left.
;
; op1 <- op1 <<< 8
; --------------------------------------------------
rot8Bits
    +move16Bit ZERO_PAGE_1, ZERO_PAGE_3
    +dec16Bit ZERO_PAGE_3

    ; save most significant byte
    ldy #3
    lda (ZERO_PAGE_1),y
    sta .SCRATCH_ROT_8

    ; copy remaining three bytes
    lda (ZERO_PAGE_3), y
    sta (ZERO_PAGE_1), y 
    dey

    lda (ZERO_PAGE_3), y
    sta (ZERO_PAGE_1), y 
    dey

    lda (ZERO_PAGE_3), y
    sta (ZERO_PAGE_1), y
    dey

    ; y reg now contains 0
    lda .SCRATCH_ROT_8
    sta (ZERO_PAGE_1),y

    rts


; --------------------------------------------------
; rot7Bit rotates the 32 bit value to which ZERO_PAGE_1 points seven bits
; to the left.
;
; op1 <- op1 <<< 7
; --------------------------------------------------
rot7Bits
    ldx #6
.loopRot7    
    jsr rot32Bit
    dex
    bpl .loopRot7

    rts


; --------------------------------------------------
; rot12Bit rotates the 32 bit value to which ZERO_PAGE_1 points twelve bits
; to the left.
;
; op1 <- op1 <<< 12
; --------------------------------------------------
rot12Bits
    jsr rot8Bits
    ldx #3
.loopRot12    
    jsr rot32Bit
    dex
    bpl .loopRot12
    
    rts


ADDR_A
!byte 0,0
ADDR_B
!byte 0,0
ADDR_C
!byte 0,0
ADDR_D
!byte 0,0
; --------------------------------------------------
; chaChaQuarterRound implements a ChaCha20 quarter round.
;
; The memory addresses of the 32 bit values have to be stored in ADDR_A - ADDR_D
; by the caller
; --------------------------------------------------
chaChaQuarterRound
    ; a += b
    +move16Bit ADDR_A, ZERO_PAGE_3
    +move16Bit ADDR_B, ZERO_PAGE_1
    jsr add32BitUnsigned
    ; d ^= a
    +move16Bit ADDR_D, ZERO_PAGE_3
    +move16Bit ADDR_A, ZERO_PAGE_1
    jsr xor32BitUnsigned
    ; d <<< 16
    +move16Bit ADDR_D, ZERO_PAGE_1
    jsr rot16Bits


    ; c += d
    +move16Bit ADDR_C, ZERO_PAGE_3
    +move16Bit ADDR_D, ZERO_PAGE_1
    jsr add32BitUnsigned
    ; b ^= c
    +move16Bit ADDR_B, ZERO_PAGE_3
    +move16Bit ADDR_C, ZERO_PAGE_1
    jsr xor32BitUnsigned
    ; b <<< 12
    +move16Bit ADDR_B, ZERO_PAGE_1
    jsr rot12Bits


    ; a += b
    +move16Bit ADDR_A, ZERO_PAGE_3
    +move16Bit ADDR_B, ZERO_PAGE_1
    jsr add32BitUnsigned
    ; d ^= a
    +move16Bit ADDR_D, ZERO_PAGE_3
    +move16Bit ADDR_A, ZERO_PAGE_1
    jsr xor32BitUnsigned
    ; d <<< 8
    +move16Bit ADDR_D, ZERO_PAGE_1
    jsr rot8Bits


    ; c += d
    +move16Bit ADDR_C, ZERO_PAGE_3
    +move16Bit ADDR_D, ZERO_PAGE_1
    jsr add32BitUnsigned
    ; b ^= c
    +move16Bit ADDR_B, ZERO_PAGE_3
    +move16Bit ADDR_C, ZERO_PAGE_1
    jsr xor32BitUnsigned
    ; b <<< 7
    +move16Bit ADDR_B, ZERO_PAGE_1
    jsr rot7Bits

    rts

!macro loadQuarterData .addr1, .addr2, .addr3, .addr4 {
    +load16BitImmediate .addr1, ADDR_A
    +load16BitImmediate .addr2, ADDR_B
    +load16BitImmediate .addr3, ADDR_C
    +load16BitImmediate .addr4, ADDR_D
}

chaChaInnerBlock
    ;QUARTERROUND(0, 4, 8, 12)
    +loadQuarterData C_0, C_4, C_8, C_12
    jsr chaChaQuarterRound
    ;QUARTERROUND(1, 5, 9, 13)
    +loadQuarterData C_1, C_5, C_9, C_13
    jsr chaChaQuarterRound
    ;QUARTERROUND(2, 6, 10, 14)
    +loadQuarterData C_2, C_6, C_10, C_14
    jsr chaChaQuarterRound
    ;QUARTERROUND(3, 7, 11, 15)
    +loadQuarterData C_3, C_7, C_11, C_15
    jsr chaChaQuarterRound
    ;QUARTERROUND(0, 5, 10, 15)
    +loadQuarterData C_0, C_5, C_10, C_15
    jsr chaChaQuarterRound
    ;QUARTERROUND(1, 6, 11, 12)
    +loadQuarterData C_1, C_6, C_11, C_12
    jsr chaChaQuarterRound
    ;QUARTERROUND(2, 7, 8, 13)
    +loadQuarterData C_2, C_7, C_8, C_13
    jsr chaChaQuarterRound
    ;QUARTERROUND(3, 4, 9, 14)
    +loadQuarterData C_3, C_4, C_9, C_14
    jsr chaChaQuarterRound

    rts


; --------------------------------------------------
; chaChaAddState adds CHACHA_INITIAL_STATE and CHACHA_STATE.
; --------------------------------------------------
chaChaAddState
    +load16BitImmediate CHACHA20_INITIAL_STATE, ZERO_PAGE_1
    +load16BitImmediate CHACHA20_STATE, ZERO_PAGE_3
    ldx #15
.loopAddState
    jsr add32BitUnsigned
    +add16BitImmediate 4, ZERO_PAGE_1
    +add16BitImmediate 4, ZERO_PAGE_3
    dex
    bpl .loopAddState

    rts

.ROUND_COUNT
!byte 0
; --------------------------------------------------
; chacha20BlockFunc implements the ChaCha20 block function
; --------------------------------------------------
chacha20BlockFunc
    lda #0
    sta .ROUND_COUNT
.loopRounds
    jsr chaChaInnerBlock
    inc .ROUND_COUNT
    lda .ROUND_COUNT
    cmp #10
    bne .loopRounds

    jsr chaChaAddState
    rts

chaChaClearKey
    +memsetImmediate 0, 32, CHACHA_KEY
    +memsetImmediate 0, 12, CHACHA_NONCE
    rts


chaChaSetInitialState
    lda #1
    sta CHACHA_BLOCK
    lda #0
    sta CHACHA_BLOCK+1
    sta CHACHA_BLOCK+2
    sta CHACHA_BLOCK+3
chaChaCopyState
    ldx #0
.copyState
    lda CHACHA20_INITIAL_STATE,x
    sta CHACHA20_STATE,x
    inx
    cpx #64
    bne .copyState    

    rts

chaChaNextInitialState
    +inc16Bit CHACHA_BLOCK
    jmp chaChaCopyState


CHACHA20_INITIAL_STATE
I_0
;0x61707865
!byte $65,$78,$70,$61  
I_1
; 0x3320646e
!byte $6e,$64,$20,$33
I_2
; 0x79622d32
!byte $32,$2d,$62,$79
I_3
; 0x6b206574
!byte $74,$65,$20,$6b

CHACHA_KEY
I_4
!byte 0,0,0,0
I_5
!byte 0,0,0,0
I_6
!byte 0,0,0,0
I_7
!byte 0,0,0,0
I_8
!byte 0,0,0,0
I_9
!byte 0,0,0,0
I_10
!byte 0,0,0,0
I_11
!byte 0,0,0,0

CHACHA_BLOCK
I_12
!byte 0,0,0,0

CHACHA_NONCE
I_13
!byte 0,0,0,0
I_14
!byte 0,0,0,0
I_15
!byte 0,0,0,0


CHACHA20_STATE
C_0
!byte 0,0,0,0
C_1
!byte 0,0,0,0
C_2
!byte 0,0,0,0
C_3
!byte 0,0,0,0
C_4
!byte 0,0,0,0
C_5
!byte 0,0,0,0
C_6
!byte 0,0,0,0
C_7
!byte 0,0,0,0
C_8
!byte 0,0,0,0
C_9
!byte 0,0,0,0
C_10
!byte 0,0,0,0
C_11
!byte 0,0,0,0
C_12
!byte 0,0,0,0
C_13
!byte 0,0,0,0
C_14
!byte 0,0,0,0
C_15
!byte 0,0,0,0


chaChaInit
    jsr chaChaSetInitialState
    jsr chacha20BlockFunc
    lda #0
    sta .KEY_STREAM_COUNT
    
    rts


.KEY_STREAM_COUNT
!byte 0
chaChaGetNextByte
    ldx .KEY_STREAM_COUNT
    cpx #64
    bne .getKeyByte
    jsr chaChaNextInitialState
    jsr chacha20BlockFunc
    ldx #0
    stx .KEY_STREAM_COUNT
.getKeyByte
    lda CHACHA20_STATE, x
    inc .KEY_STREAM_COUNT
    rts


NONCE_FIXED_LEN = 8

.DERIVATION_NONCE
!byte NONCE_FIXED_LEN              ; 8 nonce characters
!pet "cbmcrypt"
!byte 0,0,0,0                      ; room to make a full 12 byte nonce 

ADDR_SEED
!byte 0,0
ADDR_KEY_ID
!byte 0,0
ADDR_PRNG_OUT
!byte 0,0
ADDR_CHECK_VALUE
!byte 0,0

.DERIVE_OUT_COUNT
!byte 0
.DESIRED_OUT_LENGTH
!byte 0
; --------------------------------------------------
; chaChaDeriveCall uses ChaCha20 as a PRNG. It expands the seed value
; the address of which has to be specified through ADDR_SEED and the key id
; which is referenced by ADRR_KEY_ID into a
; pseudorandom value stored at ADDR_PRNG_OUT. Both parameters have to 
; be a string having a preceeding length byte. The length byte of
; ADDR_SEED determines the input length and the length byte of ADDR_PRNG_OUT
; the length of the out byte sequence. The seed can not be longer than
; 32 bytes.
; --------------------------------------------------
chaChaDeriveCall
    ; set up ChaCha20
    jsr chaChaClearKey

    ; set current length of derivation nonce to NONCE_FIXED_LEN
    lda #NONCE_FIXED_LEN
    sta .DERIVATION_NONCE
    
    ; append key id to fixed nonce value
    +move16Bit ADDR_KEY_ID, ZERO_PAGE_1
    +load16BitImmediate .DERIVATION_NONCE, ZERO_PAGE_3
    jsr strCatCall

    ; copy nonce value to cha cha state
    +load16BitImmediate .DERIVATION_NONCE, ZERO_PAGE_1
    +load16BitImmediate CHACHA_NONCE, ZERO_PAGE_3
    jsr copyByteVectorCall
    
    ; copy key seed to cha cha state
    +move16Bit ADDR_SEED, ZERO_PAGE_1
    +load16BitImmediate CHACHA_KEY, ZERO_PAGE_3
    jsr copyByteVectorCall

    jsr chaChaInit

    ; copy desired length
    +move16Bit ADDR_PRNG_OUT, ZERO_PAGE_1
    ldy #0
    lda (ZERO_PAGE_1),y
    sta .DESIRED_OUT_LENGTH

    ; initialize loop
    lda #0
    sta .DERIVE_OUT_COUNT
    +inc16Bit ADDR_PRNG_OUT
.deriveLoop
    ldy .DERIVE_OUT_COUNT
    cpy .DESIRED_OUT_LENGTH
    beq .derivationDone
    jsr chaChaGetNextByte
    pha
    +move16Bit ADDR_PRNG_OUT, ZERO_PAGE_1
    pla
    ldy .DERIVE_OUT_COUNT
    sta (ZERO_PAGE_1), y
    inc .DERIVE_OUT_COUNT
    jmp .deriveLoop

.derivationDone
    +dec16Bit ADDR_PRNG_OUT
    ; generate check value
    +move16Bit ADDR_CHECK_VALUE, ZERO_PAGE_1
    lda #3
    ldy #0
    sta (ZERO_PAGE_1), Y
    iny
    sty .DERIVE_OUT_COUNT

    jsr chaChaGetNextByte
    ldy .DERIVE_OUT_COUNT
    sta (ZERO_PAGE_1), Y
    inc .DERIVE_OUT_COUNT

    jsr chaChaGetNextByte
    ldy .DERIVE_OUT_COUNT
    sta (ZERO_PAGE_1), Y
    inc .DERIVE_OUT_COUNT

    jsr chaChaGetNextByte
    ldy .DERIVE_OUT_COUNT
    sta (ZERO_PAGE_1), Y

    rts


!macro chaChaDerive .addrSeed, .addrKeyId, .addrDerived, .addrCheckValue {
    +load16BitImmediate .addrSeed, ADDR_SEED
    +load16BitImmediate .addrDerived, ADDR_PRNG_OUT
    +load16BitImmediate .addrCheckValue, ADDR_CHECK_VALUE
    +load16BitImmediate .addrKeyId, ADDR_KEY_ID
    jsr chaChaDeriveCall   
}


ADDR_PREFIX
!byte 0,0
ADDR_MSG_COUNTER
!byte 0,0
ADDR_NONCE
!byte 0,0
.PREFIX_LEN
!byte 0,0
constructNonceCall
    ; set length to 12
    +move16Bit ADDR_NONCE, ZERO_PAGE_1
    lda #12
    ldy #0
    sta (ZERO_PAGE_1), y
    ; clear contents of target memory
    +inc16Bit ZERO_PAGE_1
    ldx #0
    ldy #12
    jsr memsetCall
    ; copy nonce prefix
    +move16Bit ADDR_PREFIX, ZERO_PAGE_1
    ldy #0
    lda (ZERO_PAGE_1),Y
    sta .PREFIX_LEN                                ; save length of prefix
    +move16Bit ADDR_NONCE, ZERO_PAGE_3
    +inc16Bit ZERO_PAGE_3
    jsr copyByteVectorCall
    ; copy message counter
    +move16Bit ADDR_MSG_COUNTER, ZERO_PAGE_1
    +move16Bit ADDR_NONCE, ZERO_PAGE_3
    +add16Bit .PREFIX_LEN, ZERO_PAGE_3
    +inc16Bit ZERO_PAGE_3
    jsr copyByteVectorCall
 
    rts

!macro constructNonce .addrPrefix, .addrMsgCount, .addrNonce {
    +load16BitImmediate .addrPrefix, ADDR_PREFIX
    +load16BitImmediate .addrMsgCount, ADDR_MSG_COUNTER
    +load16BitImmediate .addrNonce, ADDR_NONCE
    jsr constructNonceCall
}


KEY_ADDR
!byte 0,0
NONCE_ADDR
!byte 0,0
BUFFER_ADDR
!byte 0,0
NUM_BUFFER_BYTES
!byte 0,0
BUFFER_COUNT
!byte 0,0
; --------------------------------------------------
; processBufferCall encrypts/decrypts the buffer stored at .BUFFER_ADDR using
; the key (as string) stored at .KEY_ADDR and the nonce stored (as string) 
; at .NONCE_ADDR. In the buffer .NUM_BUFFER_BYTES bytes are encrypted.
; --------------------------------------------------
processBufferCall
    +load16BitImmediate 0, BUFFER_COUNT
    jsr chaChaClearKey

    +move16Bit KEY_ADDR, TEMP_PTR
    +load16BitImmediate CHACHA_KEY, TEMP_PTR2
    jsr copyByteVectorCall

    +move16Bit NONCE_ADDR, TEMP_PTR
    +load16BitImmediate CHACHA_NONCE, TEMP_PTR2
    jsr copyByteVectorCall

    jsr chaChaInit
    +move16Bit BUFFER_ADDR, ZERO_PAGE_7    
.nextByte
    +cmp16Bit BUFFER_COUNT, NUM_BUFFER_BYTES
    beq .processingDone
    jsr chaChaGetNextByte
    ldy #0
    eor (ZERO_PAGE_7), Y
    sta (ZERO_PAGE_7), y
    +inc16Bit ZERO_PAGE_7
    +inc16Bit BUFFER_COUNT
    jmp .nextByte

.processingDone
    rts

!macro processBufferImmediate .addrKey, .addrNonce, .bufferAddr, .numBytes {
    +load16BitImmediate .addrKey, KEY_ADDR
    +load16BitImmediate .addrNonce, NONCE_ADDR
    +load16BitImmediate .bufferAddr, BUFFER_ADDR
    +load16BitImmediate .numBytes, NUM_BUFFER_BYTES
    jsr processBufferCall
}

!macro processBufferAddr .addrKey, .addrNonce, .bufferAddr, .numBytes {
    +load16BitImmediate .addrKey, KEY_ADDR
    +load16BitImmediate .addrNonce, NONCE_ADDR
    +load16BitImmediate .bufferAddr, BUFFER_ADDR
    +move16Bit .numBytes, NUM_BUFFER_BYTES
    jsr processBufferCall
}


!ifdef IS_TEST {

CHACHA_TEST_KEY
!byte 32
!byte $00,$01,$02,$03,$04,$05,$06,$07,$08,$09,$0a,$0b,$0c,$0d,$0e,$0f
!byte $10,$11,$12,$13,$14,$15,$16,$17,$18,$19,$1a,$1b,$1c,$1d,$1e,$1f

CHACHA_TEST_NONCE
!byte 12
!byte $00,$00,$00,$00,$00,$00,$00,$4a,$00,$00,$00,$00

CHACHA_CHECK_VALUE
!byte 3
!byte 0,0,0

CHACHA_TEST_KEY_ID
!byte 2
!byte $11,$22

VALIDATE_LEN = 114

TEST_BUFFER_STR
!byte VALIDATE_LEN
TEST_BUFFER
!skip VALIDATE_LEN

.BUFFER_LEN
!byte <VALIDATE_LEN, >VALIDATE_LEN

chachaTest    
    +processBufferAddr CHACHA_TEST_KEY, CHACHA_TEST_NONCE, TEST_BUFFER, .BUFFER_LEN
    +printByteVector TEST_BUFFER_STR
    jsr printCRLF

    rts

CHACHA_DERIVED
!byte 32
!skip 32

CHACHA_TEST_SEED
!byte 23
!pet "aBjsdhgf/SH+w7rt8284756"

chaChaDeriveTest
    +chaChaDerive CHACHA_TEST_SEED, CHACHA_TEST_KEY_ID, CHACHA_DERIVED, CHACHA_CHECK_VALUE
    +printByteVector CHACHA_DERIVED
    jsr printCRLF
    +printByteVector CHACHA_CHECK_VALUE
    jsr printCRLF
    rts

MSG_COUNTER 
!byte 2
!byte 42, 1
TEST_PREFIX
!byte 2
!byte $d2,$ff
TEST_NONCE
!byte 12
!byte 0,0,0,0,0,0,0,0,0,0,0,0

chaChaNonceTest 
    +constructNonce TEST_PREFIX, MSG_COUNTER, TEST_NONCE
    +printByteVector TEST_NONCE
    jsr printCRLF

    rts

}
    